plugins {
    id "application"
}

group = "dev.tildejustin"
version = "1.1"

repositories {
    mavenCentral()
    maven { url "https://maven.fabricmc.net" }
}

dependencies {
    implementation "net.fabricmc:fabric-loader:0.15.7"
    implementation "org.ow2.asm:asm:9.6"
    implementation "net.sf.py4j:py4j:0.10.9.7"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release.set(8)
    }
}

jar {
    manifest {
        attributes(
                "Main-Class": "dev.tildejustin.loaderversioning.Main"
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}